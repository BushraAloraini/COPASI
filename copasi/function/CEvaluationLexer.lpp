/* scanner for kinetic functions */

%option prefix="CEvaluation"
%option never-interactive
%option c++

%s sSIGNorVALUE sOPERATOR sVALUE

%{
#include <vector>

#include "copasi.h"
#include "CEvaluationNode.h"
#include "CEvaluationLexer.h"
#include "CEvaluationParser_yacc.hpp"

#ifndef YYERRCODE
#define YYERRCODE 256
#endif

#define YY_USER_INIT \
    mpNode = NULL;\
    mPosition = 0;\
    mpNodeList = new std::vector< CEvaluationNode * >;

#define COMMON_ACTION \
    mPosition += yyleng;\
    mpNodeList->push_back(mpNode);

%}

DIGIT    [0-9]
HEX      [0-9a-fA-F]
ID       (\"([^\\\"]|\\.)*\"|[a-z_A-Z][a-z_A-Z0-9]*)
   
%%
<INITIAL,sSIGNorVALUE,sVALUE>{DIGIT}+("."?{DIGIT}*(((e|E)"+"?|(e|E)"-"){DIGIT}+)?)?  %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeNumber(CEvaluationNode::S_DOUBLE,
                                     yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
%}
 
<INITIAL,sSIGNorVALUE,sVALUE>(exponentiale|EXPONENTIALE) %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeConstant(CEvaluationNode::S_EXPONENTIALE,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(pi|PI) %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeConstant(CEvaluationNode::S_PI,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(true|TRUE) %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeConstant(CEvaluationNode::S_TRUE,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_VALUE;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(false|FALSE) %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeConstant(CEvaluationNode::S_FALSE,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_VALUE;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(infinity|INFINITY) %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeConstant(CEvaluationNode::S_INFINITY,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(nan|NAN) %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeConstant(CEvaluationNode::S_NAN,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
%}

<INITIAL,sSIGNorVALUE>(not|NOT|!)   %{
  BEGIN(sVALUE); 
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_NOT,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_NOT;
%}

<sOPERATOR>(le|LE|\<=)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_LE,
                                      yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_LE;
%}

<sOPERATOR>(lt|LT|\<)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_LT,
                                      yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_LT;
%}

<sOPERATOR>(ge|GE|\>=)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_GE,
                                      yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_GE;
%}

<sOPERATOR>(gt|GT|\>)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_GT,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_GT;
%}

<sOPERATOR>(ne|NE|!=)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_NE,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_NE;
%}

<sOPERATOR>(eq|EQ|==)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_EQ,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_EQ;
%}

<sOPERATOR>(and|AND|&&)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_AND,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_AND;
%}

<sOPERATOR>(xor|XOR)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_XOR,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_XOR;
%}

<sOPERATOR>(or|OR|\|\|)   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeLogical(CEvaluationNode::S_OR,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_OR;
%}

<INITIAL,sSIGNorVALUE,sVALUE>\[([^\\\]]|\\.)*\]  %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeUnit(CEvaluationNode::S_DEFAULT,
                                   yytext);
  COMMON_ACTION;
  return TOKEN_UNIT;
%}

<INITIAL,sSIGNorVALUE,sVALUE>\<([^\\\>]|\\.)*\>  %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeObject(CEvaluationNode::S_CN,
                                     yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
%}
     
<INITIAL,sSIGNorVALUE,sVALUE>0x{HEX}+  %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeObject(CEvaluationNode::S_POINTER,
                                     yytext);
  COMMON_ACTION;
  return TOKEN_NUMBER;
 %}
		  
<INITIAL,sSIGNorVALUE,sVALUE>(log|LOG)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_LOG,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(log10|LOG10)/\( %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_LOG10,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(exp|EXP)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_EXP,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(sin|SIN)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_SIN,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(cos|COS)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_COS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(tan|TAN)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_TAN,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(sec|SEC)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_SEC,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(csc|CSC)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_CSC,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(cot|COT)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_COT,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(sinh|SINH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_SINH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(cosh|COSH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_COSH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(tanh|TANH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_TANH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(sech|SECH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_SECH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(csch|CSCH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_CSCH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(coth|COTH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_COTH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(asin|ASIN)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCSIN,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(acos|ACOS)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCCOS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(atan|ATAN)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCTAN,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arcsec|ARCSEC)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCSEC,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arccsc|ARCCSC)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCCSC,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arccot|ARCCOT)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCCOT,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arcsinh|ARCSINH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCSINH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arccosh|ARCCOSH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCCOSH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arctanh|ARCTANH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCTANH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arcsech|ARCSECH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCSECH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}
 
<INITIAL,sSIGNorVALUE,sVALUE>(arccsch|ARCCSCH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCCSCH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(arccoth|ARCCOTH)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ARCCOTH,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(sqrt|SQRT)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_SQRT,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(abs|ABS)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_ABS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(floor|FLOOR)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_FLOOR,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(ceil|CEIL)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_CEIL,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(factorial|FACTORIAL)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_FACTORIAL,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(uniform|UNIFORM)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_RUNIFORM,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION_2;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(gamma|GAMMA)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_RGAMMA,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION_2;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(poisson|POISSON)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_RPOISSON,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(normal|NORMAL)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_RNORMAL,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION_2;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(max|MAX)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_MAX,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION_2;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(min|MIN)/\(   %{
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_MIN,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION_2;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(delay|DELAY)/\(   %{
  mpNode = new CEvaluationNodeDelay(CEvaluationNode::S_DELAY,
                                    yytext);
  COMMON_ACTION;
  return TOKEN_FUNCTION_2;
%}

<INITIAL,sSIGNorVALUE,sVALUE>(if|IF)/\(   %{
  mpNode = new CEvaluationNodeChoice(CEvaluationNode::S_IF,
                                     yytext);
  COMMON_ACTION;
  return TOKEN_LOGICAL_CHOICE;
%}

<INITIAL,sSIGNorVALUE,sVALUE>{ID}\(/\) %{
    { 
      std::string tmp(yytext);
      mpNode = new CEvaluationNodeCall(CEvaluationNode::S_EXPRESSION,
                                       tmp.substr(0, tmp.length() - 1));
    }
  COMMON_ACTION;
  return TOKEN_CALL;
%}

<INITIAL,sSIGNorVALUE,sVALUE>{ID}\( %{
    { 
      std::string tmp(yytext);
      mpNode = new CEvaluationNodeCall(CEvaluationNode::S_FUNCTION,
                                       tmp.substr(0, tmp.length() - 1));
    }
  COMMON_ACTION;
  return TOKEN_CALL;
%}

<INITIAL,sSIGNorVALUE>"-"   %{
  BEGIN(sVALUE); 
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_MINUS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_SIGN;
%}

<INITIAL,sSIGNorVALUE>"+"   %{
  BEGIN(sVALUE); 
  mpNode = new CEvaluationNodeFunction(CEvaluationNode::S_PLUS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_SIGN;
%}

<sOPERATOR>"^"   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_POWER,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_POWER;
%}

<sOPERATOR>"*"   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_MULTIPLY,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_MULTIPLY;
%}

<sOPERATOR>"/"   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_DIVIDE,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_MULTIPLY;
%}

<sOPERATOR>"%"   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_MODULUS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_MODULUS;
%}

<sOPERATOR>"mod"   %{
  BEGIN(sSIGNorVALUE); 
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_REMAINDER,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_REMAINDER;
%}

<sOPERATOR>"+"   %{
  BEGIN(sSIGNorVALUE);
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_PLUS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_PLUS;
%}

<sOPERATOR>"-"   %{
  BEGIN(sSIGNorVALUE);
  mpNode = new CEvaluationNodeOperator(CEvaluationNode::S_MINUS,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_OPERATOR_PLUS;
%}

"("   %{
  BEGIN(sSIGNorVALUE); 
  mPosition += yyleng;
  // mpNode = new CEvaluationNodeStructure(CEvaluationNode::S_OPEN,
  //                                       yytext);
  // COMMON_ACTION;
  return TOKEN_STRUCTURE_OPEN;
%}

"{"   %{
  BEGIN(sSIGNorVALUE); 
  mPosition += yyleng;
  // mpNode = new CEvaluationNodeStructure(CEvaluationNode::S_VECTOR_OPEN,
  //                                       yytext);
  // COMMON_ACTION;
  return TOKEN_STRUCTURE_VECTOR_OPEN;
%}

","   %{
  BEGIN(sSIGNorVALUE); 
  mPosition += yyleng;
  // mpNode = new CEvaluationNodeStructure(CEvaluationNode::S_COMMA,
  //                                       yytext);
  // COMMON_ACTION;
  return TOKEN_STRUCTURE_COMMA;
%}

")"   %{
  BEGIN(sOPERATOR); 
  mPosition += yyleng;
  // mpNode = new CEvaluationNodeStructure(CEvaluationNode::S_CLOSE,
  //                                       yytext);
  // COMMON_ACTION;
  return TOKEN_STRUCTURE_CLOSE;
%}

"}"   %{
  BEGIN(sOPERATOR); 
  mPosition += yyleng;
  // mpNode = new CEvaluationNodeStructure(CEvaluationNode::S_VECTOR_CLOSE,
  //                                       yytext);
  // COMMON_ACTION;
  return TOKEN_STRUCTURE_VECTOR_CLOSE;
%}

<INITIAL,sSIGNorVALUE,sVALUE>{ID}  %{
  BEGIN(sOPERATOR); 
  mpNode = new CEvaluationNodeVariable(CEvaluationNode::S_DEFAULT,
                                       yytext);
  COMMON_ACTION;
  return TOKEN_VARIABLE;
%}

[ \t\r\n]+ %{
  mPosition += yyleng;
  // mpNode = new CEvaluationNodeWhiteSpace(CEvaluationNode::S_DEFAULT,
  //                                        yytext);
  // COMMON_ACTION;
%}

<<EOF>>  return 0;

.     %{
  CCopasiMessage(CCopasiMessage::ERROR, MCFunction + 2, mPosition);
  return YYERRCODE;
%}
     
%%

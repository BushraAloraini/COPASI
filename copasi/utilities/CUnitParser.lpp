/* scanner for kinetic functions */

%option outfile="CUnitParser_lex.cpp"
%option prefix="CUnitParser"
%option never-interactive
%option c++

%{
#include <vector>

#include "copasi.h"
#include "CUnitParser.h"
#include "CUnitParser_yacc.hpp"

#include "utilities/CCopasiMessage.h"
#include "utilities/utility.h"

#ifndef YYERRCODE
#define YYERRCODE 256
#endif

#define YY_USER_INIT \
    mPosition = 0;


#define COMMON_ACTION \
    mData.pUnit = NULL; \
    mData.text = yytext; \
    mPosition += yyleng;

%}

SCALE    [afpumcdhkMGTP]
KIND     ([mgsAK#]|cd)
DIGIT    [0-9]

%%
{SCALE}/{KIND}   %{
  COMMON_ACTION;
  return SCALE;
%}

{KIND}   %{
  COMMON_ACTION;
  return KIND;
%}

{DIGIT}+("."?{DIGIT}*(((e|E)"+"?|(e|E)"-"){DIGIT}+)?)?   %{
  COMMON_ACTION;
  return NUMBER;
%}

"*"   %{
  COMMON_ACTION;
  return MULTIPLY;
%}

"/"   %{
  COMMON_ACTION;
  return DIVIDE;
%}

"("   %{
  COMMON_ACTION;
  return START_PARENS;
%}

")"   %{
  COMMON_ACTION;
  return END_PARENS;
%}

"^"   %{
  COMMON_ACTION;
  return EXPONENT;
%}

[ \t\r\n]+ %{
  COMMON_ACTION;
%} 

<<EOF>>  return 0;

.     %{
  CCopasiMessage(CCopasiMessage::ERROR, MCFunction + 2, mPosition);
  return YYERRCODE;
%}
     
%%

// The bison parser together with CEvaluationFlexLexer builds
// the evaluation tree.

%{
#define YYSTYPE CUnitParserBase::Data

#ifdef yylval
# undef yylval
#endif
#define yylval mData

#ifdef yyerror
# undef yyerror
#endif
#define yyerror(__str) \
  if (yychar != YYERRCODE) correctErrorPosition(); \
  CCopasiMessage(CCopasiMessage::ERROR, MCFunction + 1, mPosition)

#include "copasi.h"
#include "CUnitParser.h"

#include "utilities/CCopasiMessage.h"
#include "utilities/utility.h"

#undef yyparse
#define yyparse CUnitParserBase::yyparse
%}

/* yacc Declarations */
%token SCALE
%token KIND
%token SI_UNIT
%token NUMBER
%token MULTIPLY
%token DIVIDE
%token START_PARENS
%token END_PARENS
%token EXPONENT

// Precedence
%left SCALE
%left KIND
%left SI_UNIT
%left NUMBER
%left MULTIPLY
%left DIVIDE
%left START_PARENS
%left END_PARENS
%right EXPONENT

/* Grammar follows */
%%

list_of_components:  component
           {
             $$.pUnit = new CUnit("");
             $$.pUnit->addComponent($$.component);
             mData = $$;
           }
         | scaled_si_unit
           { 
             $$.pUnit = $1.pUnit;
             mData = $$;
           }
         | NUMBER MULTIPLY scaled_si_unit
           { 
             $$.component.setKind(CBaseUnit::dimensionless);
             $$.component.setMultiplier(strToDouble($1.text.c_str(), NULL));
             $$.pUnit = $3.pUnit;
             $$.pUnit->addComponent($$.component);
             mData = $$;
           }
         | list_of_components MULTIPLY list_of_components
           {
             $$.pUnit = new CUnit("");
             *$$.pUnit = *$1.pUnit * *$3.pUnit;
             mData = $$;
             pdelete($1.pUnit);
             pdelete($3.pUnit);
           }
         | list_of_components DIVIDE list_of_components
           {
             $$.pUnit = new CUnit("");
             *$$.pUnit = *$1.pUnit * $3.pUnit->exponentiate(-1);
             mData = $$;
             pdelete($1.pUnit);
             pdelete($3.pUnit);
           }
         | NUMBER DIVIDE list_of_components
           {
             $$.component.setKind(CBaseUnit::dimensionless);
             $$.component.setMultiplier(strToDouble($1.text.c_str(), NULL));
             $$.pUnit = $3.pUnit;
             $$.pUnit->exponentiate(-1);
             $$.pUnit->addComponent($$.component);
             mData = $$;
           }
         | list_of_components DIVIDE NUMBER
           {
             $$.component.setKind(CBaseUnit::dimensionless);
             $$.component.setMultiplier(1.0/strToDouble($1.text.c_str(), NULL));
             $$.component.setExponent(-1.0);
             $$.pUnit = $1.pUnit;
             $$.pUnit->addComponent($$.component);
             mData = $$;
           }
         | START_PARENS list_of_components END_PARENS
           {
             $$ = $2;
             mData = $$;
           }
         | list_of_components EXPONENT NUMBER
           {
             $1.pUnit->exponentiate(strToDouble($3.text.c_str(), NULL));
             $$.pUnit = $1.pUnit; 
             mData = $$;
           }

component: scaled_kind
         | NUMBER MULTIPLY scaled_kind
           {
             $$ = $3;
             $$.component.setMultiplier(strToDouble($1.text.c_str(), NULL));
           }

scaled_kind: KIND
           {
             $$.component.setKind(CBaseUnit::fromSymbol($1.text));
           }
         | SCALE KIND
           {
             $$.component.setScale(CBaseUnit::scaleFromPrefix($1.text));
             $$.component.setKind(CBaseUnit::fromSymbol($2.text));
           }

scaled_si_unit: SI_UNIT
           {
             $$.pUnit = new CUnit(CUnit::getSIUnit($1.text));
           }
         | SCALE SI_UNIT
           {
             $$.component.setKind(CBaseUnit::dimensionless);
             $$.component.setScale(CBaseUnit::scaleFromPrefix($1.text));
             $$.pUnit = new CUnit(CUnit::getSIUnit($2.text));
             $$.pUnit->addComponent($$.component);
           }
%%

